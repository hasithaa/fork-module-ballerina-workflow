// Copyright (c) 2026, WSO2 LLC. (https://www.wso2.com) All Rights Reserved.
//
// WSO2 LLC. licenses this file to you under the Apache License,
// Version 2.0 (the "License"); you may not use this file except
// in compliance with the License.
// You may obtain a copy of the License at
//
//    http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied. See the License for the
// specific language governing permissions and limitations
// under the License.

import ballerina/ai;
import ballerina/jballerina.java;
import ballerina/log;

# Starts the workflow runtime after all workflows have been registered.
#
# This is an **internal** function used by the compiler plugin as the last
# generated module-level statement, ensuring that polling only begins after
# every `registerWorkflow` call has executed.
#
# + return - `true` if the worker started successfully, or an error if starting fails
public isolated function startWorkflowRuntime() returns boolean|error {
    error? err = startWorkflowRuntimeNative();
    if err is error {
        log:printError("Workflow runtime failed to start", 'error = err);
        return err;
    }
    return true;
}

# Native call to start the singleton worker.
# + return - An error if starting fails, otherwise nil
isolated function startWorkflowRuntimeNative() returns error? = @java:Method {
    'class: "io.ballerina.lib.workflow.worker.WorkflowWorkerNative",
    name: "startSingletonWorker"
} external;

# Registers a module-level `final` client object so that it can be passed as
# an argument to activity functions whose parameter type is a client object.
#
# This is an **internal** function used by the compiler plugin. It is emitted
# by the source modifier for every module-level `final` variable whose type is a
# `client object` and is invoked during module initialization, before
# `startWorkflowRuntime`.
#
# When an activity is called with such a client as one of its arguments, the
# native runtime substitutes the value with the marker string
# `"connection:<name>"` for transport, then resolves it back to the registered
# client on the activity worker side using the same name.
#
# + name - The Ballerina variable name of the client (used as the lookup key)
# + connection - The client object reference to register
# + return - `true` on success. Re-registering the same client object under the
#            same name is idempotent and also returns `true`; an error is
#            returned only if a different client is already registered there.
public isolated function registerConnection(string name, object {} connection)
        returns boolean|error = @java:Method {
    'class: "io.ballerina.lib.workflow.worker.WorkflowWorkerNative",
    name: "registerConnection"
} external;

# Hands the build-time workflow descriptor (workflow.def.json) to the runtime as data.
#
# This is an **internal** function: the compiler plugin embeds the canonical descriptor
# document in the generated `__registerWorkflowsAndStart()` so the runtime can register
# every described workflow, activity, and human task and resolve the implementation
# functions by their recorded coordinates when the worker starts. This single data-only
# call replaces the former per-workflow `registerWorkflow`/`registerHumanTask` codegen.
#
# + descriptorJson - The canonical descriptor document
# + return - `true` on success, or an error when the document is not valid JSON
public isolated function registerWorkflowDescriptor(string descriptorJson)
        returns boolean|error = @java:Method {
    'class: "io.ballerina.lib.workflow.runtime.nativeimpl.WorkflowDescriptorNative",
    name: "registerWorkflowDescriptor"
} external;

// ---------------------------------------------------------------------------
// Object-model durable agent declaration registration
// ---------------------------------------------------------------------------
// These are **internal** functions emitted by the compiler plugin at module init
// for every module-level `final workflow:DurableAgent x = new ({...})`
// declaration. The plugin decomposes the constructor config into these calls;
// the runner workflow resolves the declaration by agent name at run time.

# Registers a durable agent declaration: identity, model, system prompt, and
# reasoning limit. Must be called before the capability registrations below.
#
# + agentName - The agent's name (its module-level variable name)
# + model - The agent's `ai:ModelProvider`
# + systemPrompt - The agent's system prompt (`role` + `instructions`)
# + maxIter - Per-turn reasoning iteration cap
# + inputType - The type of the structured JSON payload accepted alongside the
#               query: `json` for any payload, a narrower type to validate its
#               shape, or `()` for a query-only agent
# + resultType - The agent's declared result type, or `()` for the final text response
# + return - `true` on success, or an error for a duplicate agent name
public isolated function registerDurableAgentDecl(string agentName, ai:ModelProvider model,
        json systemPrompt, int maxIter, typedesc<json>? inputType = json,
        typedesc<anydata>? resultType = (), json eventTimeout = ()) returns boolean|error = @java:Method {
    'class: "io.ballerina.lib.workflow.runtime.nativeimpl.DurableAgentNative",
    name: "registerDurableAgentDecl"
} external;

# Registers an activity capability declaration of a durable agent.
#
# + agentName - The agent's name
# + toolName - The tool name advertised to the model
# + activity - The `@workflow:Activity` function
# + meta - Declaration metadata (description, gating, retry policy)
# + bindings - Arguments fixed at registration, keyed by parameter name; client
#              objects are passed by reference to their module-level variable
# + return - `true` on success, or an error for an unknown agent
public isolated function registerDurableAgentActivity(string agentName, string toolName,
        function activity, json meta = (), map<anydata|object {}>? bindings = ())
        returns boolean|error = @java:Method {
    'class: "io.ballerina.lib.workflow.runtime.nativeimpl.DurableAgentNative",
    name: "registerDurableAgentActivity"
} external;

# Registers an event channel declaration of a durable agent.
#
# + agentName - The agent's name
# + eventName - The channel name
# + request - The channel's request type
# + response - The channel's response type; `()` for one-way channels
# + cardinality - `"MULTI_EVENT"` (default) or `"SINGLE_EVENT"`
# + return - `true` on success, or an error for an unknown agent
public isolated function registerDurableAgentEvent(string agentName, string eventName,
        typedesc<anydata> request, typedesc<anydata>? response = (),
        string cardinality = "MULTI_EVENT") returns boolean|error = @java:Method {
    'class: "io.ballerina.lib.workflow.runtime.nativeimpl.DurableAgentNative",
    name: "registerDurableAgentEvent"
} external;

# Registers a human task capability declaration of a durable agent.
#
# + agentName - The agent's name
# + taskName - The task name
# + meta - Declaration metadata (roles, title, description)
# + resultType - Expected result type; drives form schema generation
# + taskInputType - Declared input shape; the agent-supplied input is checked against it.
#                   Nil keeps the open default
# + return - `true` on success, or an error for an unknown agent
public isolated function registerDurableAgentHumanTask(string agentName, string taskName,
        json meta = (), typedesc<anydata> resultType = anydata,
        typedesc<map<json>>? taskInputType = ()) returns boolean|error = @java:Method {
    'class: "io.ballerina.lib.workflow.runtime.nativeimpl.DurableAgentNative",
    name: "registerDurableAgentHumanTask"
} external;

# Registers an AI tool of an object-model durable agent: stored on the declaration
# (so the runner can advertise it to the model) and published to the agent tool
# registry (so the built-in `executeAgentTool` activity can resolve it).
#
# + agentName - The agent's name (its module-level variable name)
# + tool - The tool: an `@ai:AgentTool` function, an `ai:ToolConfig`, or a toolkit
# + requiresApproval - When `true`, a `PRE_RUN` review activity gates every call
# + userRoles - Role(s) permitted to decide reviews of this tool
# + return - `true` on success, or an error
public isolated function registerDurableAgentTool(string agentName,
        ai:BaseToolKit|ai:ToolConfig|ai:FunctionTool tool, boolean requiresApproval = false,
        string|string[]? userRoles = ()) returns boolean|error {
    ai:ToolConfig[] configs;
    boolean isMcp = tool is ai:McpBaseToolKit;
    if tool is ai:BaseToolKit {
        configs = tool.getTools();
    } else if tool is ai:ToolConfig {
        configs = [tool];
    } else {
        configs = ai:getToolConfigs([tool]);
        if configs.length() == 0 {
            return error("Agent tool functions must be annotated with @ai:AgentTool");
        }
    }
    boolean result = true;
    foreach ai:ToolConfig config in configs {
        map<json>? parameters = config.parameters;
        json meta = {
            description: config.description,
            parameters: parameters is () ? () : parameters.toJsonString(),
            requiresApproval,
            userRoles,
            isMcp
        };
        boolean registered =
            check registerDurableAgentToolNative(agentName, config.name, config.caller, meta);
        result = result && registered;
    }
    return result;
}

isolated function registerDurableAgentToolNative(string agentName, string toolName, function tool,
        json meta) returns boolean|error = @java:Method {
    'class: "io.ballerina.lib.workflow.runtime.nativeimpl.DurableAgentNative",
    name: "registerDurableAgentTool"
} external;

# Registers the shared object-model runner as an agent's workflow: the agent gets
# its own workflow type (`workflow-<agentName>`) whose activities are the agent's
# declared activity functions plus the built-in agent activities. The runner and
# the built-ins are captured natively at workflow-module init, so callers pass
# only the agent name — no runner machinery leaks into any public API.
#
# + agentName - The agent's name (its module-level variable name)
# + return - `true` on success, or an error
public isolated function registerDurableAgentRunner(string agentName)
        returns boolean|error = @java:Method {
    'class: "io.ballerina.lib.workflow.runtime.nativeimpl.DurableAgentNative",
    name: "registerDurableAgentRunner"
} external;

# Registers a peer-agent declaration of an object-model durable agent: the peer is
# advertised to the agent's model as a delegable tool and runs as a true Temporal
# child workflow of the agent.
#
# + agentName - The declaring agent's name
# + peerName - The tool name advertised to the model
# + targetAgent - The peer agent's name (its module-level variable name)
# + meta - Declaration metadata (description, wait, callbackChannel, gating)
# + return - `true` on success, or an error
public isolated function registerDurableAgentPeer(string agentName, string peerName,
        string targetAgent, json meta = ()) returns boolean|error = @java:Method {
    'class: "io.ballerina.lib.workflow.runtime.nativeimpl.DurableAgentNative",
    name: "registerDurableAgentPeer"
} external;
