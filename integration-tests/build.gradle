/*
 * Copyright (c) 2026, WSO2 LLC. (https://www.wso2.com) All Rights Reserved.
 *
 * WSO2 LLC. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'jacoco'
}

apply plugin: 'java'

description = 'Ballerina - Workflow Integration Tests'

def packageName = "workflow"
def packageOrg = "ballerina"
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")
def ballerinaTomlFile = new File("$project.projectDir/Ballerina.toml")
def originalBallerinaToml = ballerinaTomlFile.text
def distributionBinPath = project.rootDir.absolutePath + "/target/ballerina-runtime/bin"
def testCoverageParam = "--code-coverage --coverage-format=xml --includes=io.ballerina.stdlib.workflow.*:ballerina.workflow*"

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-')
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", "")
    }
}

configurations {
    jbalTools
}

dependencies {
    jbalTools("org.ballerinalang:jballerina-tools:${ballerinaLangVersion}") {
        transitive = false
    }
}

clean {
    delete "$project.projectDir/target"
}

task updateTomlFiles {
    doLast {
        def newBallerinaToml = ballerinaTomlFile.text.replace("@project.version@", project.version)
        newBallerinaToml = newBallerinaToml.replace("@toml.version@", tomlVersion)
        ballerinaTomlFile.text = newBallerinaToml
    }
}

task revertTomlFiles {
    doLast {
        ballerinaTomlFile.text = originalBallerinaToml
    }
}

// Push workflow bala to local Ballerina repository for dependency resolution
task pushBalaToLocal {
    dependsOn ":${packageName}-ballerina:build"
    
    doLast {
        def balaFile = file("${project(':workflow-ballerina').projectDir}/build/bal_build_target/bala/ballerina-workflow-java21-${tomlVersion}.bala")
        if (balaFile.exists()) {
            exec {
                workingDir project.rootDir
                if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                    commandLine 'cmd', '/c', "${distributionBinPath}/bal.bat push --repository=local " +
                            "${balaFile.absolutePath} && exit %%ERRORLEVEL%%"
                } else {
                    commandLine 'sh', '-c', "${distributionBinPath}/bal push --repository=local ${balaFile.absolutePath}"
                }
            }
            logger.lifecycle("Pushed workflow bala to local repository: ${balaFile.absolutePath}")
        } else {
            logger.warn("Bala file not found: ${balaFile.absolutePath}")
        }
    }
}

def debugParams = ""
def balJavaDebugParam = ""
def testParams = ""

task initializeVariables {
    if (project.hasProperty("debug")) {
        debugParams = "--debug ${project.findProperty("debug")}"
    }
    if (project.hasProperty("balJavaDebug")) {
        balJavaDebugParam = "BAL_JAVA_DEBUG=${project.findProperty("balJavaDebug")}"
    }

    gradle.taskGraph.whenReady { graph ->
        if (graph.hasTask(":${packageName}-integration-tests:test")) {
            testParams = "${testCoverageParam}"
        }
    }
}

task ballerinaTest {
    inputs.dir file(project.projectDir)
    dependsOn(":${packageName}-ballerina:build")
    dependsOn(pushBalaToLocal)
    dependsOn(updateTomlFiles)
    dependsOn(initializeVariables)
    dependsOn(":startSharedTestServer")  // Use shared test server from root project
    // Explicit dependencies to avoid Gradle validation errors
    mustRunAfter(compileJava)
    mustRunAfter(compileTestJava)
    mustRunAfter(processResources)
    mustRunAfter(processTestResources)
    mustRunAfter(jar)
    finalizedBy(revertTomlFiles)

    doLast {
        exec {
            workingDir project.projectDir
            environment "JAVA_OPTS", "-DBALLERINA_DEV_COMPILE_BALLERINA_ORG=true"
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "${balJavaDebugParam} ${distributionBinPath}/bal.bat test " +
                        "--offline ${testParams} ${debugParams} && " +
                        "exit %%ERRORLEVEL%%"
            } else {
                commandLine 'sh', '-c', "${balJavaDebugParam} ${distributionBinPath}/bal test " +
                        "--offline ${testParams} ${debugParams}"
            }
        }
    }
}

test.dependsOn ballerinaTest
build.dependsOn test
